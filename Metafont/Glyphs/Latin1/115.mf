input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0073 LATIN SMALL LETTER S - CODE 115
newchar(115, 4.7pt#, 0);
spine_center_y = 0.5 * X_TOP;
  spine_angle:= lerpbold(14, 6);
  spine_straight_length:= lerpbold(0.1w, 0.05w);
  spine_ydelta:= 0.25 * X_H;
  right_terminal_xoffset:=0.1w;

if MONOSPACE = true:
  beak_spine_xdelta = 0.02w;
  x1r = lerpbold(w - right_terminal_xoffset, x5l); y1 = lerpbold(X_TOP - (X_H * APERTURE), X_TOP - (CAP_H * APERTURE * 0.85));
  x7l = lerpbold(x3r, 0.05w); y7 = lerpbold(BASELINE + (X_H * APERTURE), BASELINE + (CAP_H * APERTURE * 0.85));
else:
  beak_spine_xdelta = lerpbold(0.02w, 0.05w);
  x1r = lerpbold(w - right_terminal_xoffset, x5l); y1 = X_TOP - (X_H * APERTURE);
  x7l = lerpbold(x3r, 0.05w); y7 = BASELINE + (X_H * APERTURE);
fi

  x2 = x4 + beak_spine_xdelta; y2r = X_TOP;
  x3r = CURVE_BEARING; y3 = y2l - ((y2l - y41l) / 2);
  x4 = 0.5w; y4 = spine_center_y;
  z41 = z4 shifted(-spine_straight_length, 0) rotatedaround((z4), -spine_angle);
z42 = z4 shifted(spine_straight_length, 0) rotatedaround((z4), -spine_angle);

if MONOSPACE = true:
  x5l = w - CURVE_BEARING; y5 = y42r - ((y42r - y6r) / 2);
else:
  x5l = w - OBLIQUE_BEARING; y5 = y42r - ((y42r - y6r) / 2);
fi
  
  x6 = x4 - beak_spine_xdelta; y6l = CAP_BOTTOM;
  
  penpos1(BAR, 90 - APERTURE_ANGLE);
  penpos2(BAR, 90);
  penpos3(STEM, 180);
  penpos41(BAR, 270 - spine_angle);
  penpos42(BAR, 270 - spine_angle);
  penpos5(STEM, 180);
  penpos6(BAR, 90);
  penpos7(BAR, 90 - APERTURE_ANGLE);

  if SERIF = true:
    makeserif(1, z1r, -SERIF_THICKNESS, -TERM_LENGTH);
    makeserif(2, z7l,  SERIF_THICKNESS,  TERM_LENGTH);
  fi

  %penstroke z1e..tension TSN..z2e{left}..tension TSN..z3e{down}..tension TSN..z41e{dir -spine_angle} -- z42e{dir -spine_angle}..tension TSN..z5e{down}..tension TSN..z6e{left}..tension TSN..z7e;

  fill z2r{rt}...z1r -- z1l...z2l{lt}...z3l{dn}..tsn..z41l{dir -spine_angle} -- z42l{dir -spine_angle}..tsn..z5l{dn}..tsn..z6l{lt}...z7l -- z7r...z6r{rt}...z5r{up}..tsn..z42r{dir (180 - spine_angle)} -- z41r{dir (180 - spine_angle)}..tsn..z3r{up}..tsn..{rt}cycle;
  
italicize;

  penlabels (range 1 thru 42);
endchar;
end
