input ../Config/ExploreSansCFG.mf;

export_setup;

% U+006D LATIN SMALL LETTER M | CODE 109
newchar(109, 8.2pt#, 2);
if MONOSPACE = false:
  z1l = (LINE_BEARING, X_H);
  z2l = (LINE_BEARING, BASELINE);
  z9r = (w - LINE_BEARING, y5r);
  z3l = (x1r, X_H - (CAP_H * APERTURE));
  z5 = (x3l + ((x9l - x3l) / 2), ARCHHEIGHT);
else: if MONOSPACE = true:
    z1l = (CURVE_BEARING, X_H);
    z2l = (CURVE_BEARING, BASELINE);
    z9r = (w - CURVE_BEARING, y5r);
    z3l = (x1r, lerpbold(0.775 * X_H, y51));
    z5 = (x3l + ((x9l - x3l) / 2), 0.775 * X_H);

    z51 = z5l shifted(0, lerpbold(0, -0.3pt));
    z91 = z9l shifted(0, lerpbold(0, -0.3pt));
  fi
fi

z4r = (lerp(x3l, x5l, ARCHCOEF), X_TOP);
z6r = (x5r, BASELINE);

z7l = (x5r, y3l);
z10r = (x9r, BASELINE);
z8r = (lerp(x7l, x10l, ARCHCOEF), y4r);

forsuffixes $ = 1, 2, 5, 6, 9, 10:
  if MONOSPACE = true:
    penpos$(lerpbold(STEM, BAR), 0);
  else:
    penpos$(STEM, 0);
  fi
endfor;

penpos3(BAR_FINE, 90 + APERTURE_ANGLE); penpos4(BAR_CROSS, 90); 
penpos7(BAR_FINE, 90 + APERTURE_ANGLE); penpos8(BAR_CROSS, 90);

z41 = z4r shifted(STRETCH * ARCHCORR, 0);
z81 = z8r shifted(STRETCH * ARCHCORR, 0);

fill z1l -- z1r -- z2r -- z2l -- cycle;

if MONOSPACE = false:
  fill z3r...z41{right}..tsn..z7r & z7r...z81{right}..tsn..z9r{down} -- z10r -- z10l -- z9l{up}..tsn..z8l{lt}...z7l -- z5r -- z6r -- z6l -- z5l{up}..tsn..z4l{left}...z3l -- cycle;
else:
  if MONOSPACE = true:
     fill z3r{daa}..invtsn..z41{right}..tsn..z7r & z7r..invtsn..z81{right}..tsn..z9r{down} -- z10r -- z10l -- z91{up}...z8l{left}...{dn}z7l -- z5r -- z6r -- z6l -- z51{up}...z4l{left}...{dn}z3l{dia} -- cycle;
  fi
fi

italicize;

penlabels(range 1 thru 99);

endchar;
end
