input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0067 LATIN SMALL LETTER G | CODE 103
newchar(103, 5.5pt#, 0);
  ubox = 0.15w;
  ubwidth = 0.55w;
  ubheight = 0.35h;
  ubtension:= 0.9;
  lbey = -.05h;
  lboy = .05h;
  tlendx = 0.9w;
  tlangle = 30;
  %lbox

  % upper bowl
  x1 = ubox + (ubwidth / 2); y1l = X_H + OVERSHOOT;
  x2 = ubox + ubwidth; y2 = y1 - (ubheight / 2);
  x3 = x1; y3 = y1 - ubheight;
  x4 = ubox; y4 = y2;

  % spinal cord & lower bowl
  x5 = x3; y5l = y3l;                      % Keep spine base attached
  x6r = x4l; y6 = y5l - ((y5l - y7l) / 2); % Align vertical center of spine to middle xof 2 bowls
  x7r = x4r + ((x3 - x4) / 2); y7 = lboy;  % put on BASELINE;
  x8l = x2r;  y8 = y7;
  x9l = tlendx; y9 = lbey;
  x10 = x1; y10l = DESCENDER;
  x11l = x4l; y11 = -.1h;
  x117r = x7r; y117r = y7r;
  
  % tail
  x12 = x1 + (x2 - x1) / 2; y12l = y1r;
  %z13 = z12 shifted(1pt#, 0) rotatedaround(z12, 45);
  
  penpos1(mediumx, -90);
  penpos2(mediumy, -180);
  penpos3(mediumx, -270);
  penpos4(mediumy, 0);

  penpos5(txhinx, -270);
  penpos6(mediumy, -180);
  penpos7(mediumx, -90);
  penpos8(mediumx, -90);
  penpos9(mediumy, -180);
  penpos10(mediumx, -270);
  penpos11(mediumx, 0);
  penpos117(thinx, -40);

  penpos12(mediumx, 90 + tlangle);

  
  z13 = z12 shifted(0.35w, 0) rotatedaround(z12, tlangle);
  penpos13(mediumx, 90 + tlangle);

  penstroke z1e{right}..tension ubtension..z2e{down}..tension TSN..z3e{left}..tension TSN..z4e{up}..tension TSN..cycle;
  %penstroke z5e{dir -180}..z6e{down}..z7e{right} -- z8e{right}..tension 1..z9e{down}..tension 1..z10e{left}..z11e{up}..z117e{dir 0};
  penstroke z12e -- z13e;

  fill z3{left}..z6r..z7r{right} -- z8r{right}..z9r..tension (1/TSN)..z10r{left}...z11r..z117r -- z117l..z11l..z10l..tension (1/TSN)..z9l..z8l{left} -- z7l{left}.. {left}flex(z7l, z6l, z5l) -- cycle;
  
  penlabels(range 1 thru 117);
endchar;
end
