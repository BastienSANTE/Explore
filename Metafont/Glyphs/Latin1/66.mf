input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0042 LATIN CAPITAL LETTER B - CODE 66

newchar(66, 6.1pt#, 0);
  stem_x:= 0.2w;
  btension:= 0.9;

  x0 = x1; y0 = y4;
  x1l = LINE_BEARING; y1r = CAP_H;
  x2 = 0.535w; y2r = CAP_H;
  x3 = 0.82w ; y3 = 0.66h;
  x4 = x2; y4 = 0.465h;
  x41 = x2; y41l = y4l;
  x42 = x2; y42l = y4r;
  x5r = 0.88w; y5 = 0.25h;
  x6 = x2; y6r = BASELINE;
  x7 = x8; y7r = BASELINE;
  x8l = LINE_BEARING; y8 = y1r;
  x9l = LINE_BEARING; y9 = y7r;


  if MONOSPACE = true:
    x5r:= w - (CURVE_BEARING / 2);
  else:
    x5r:= 0.88w;
  fi

  penpos0(mediumx, -90);
  
  penpos1(mediumx, 90);
  penpos2(mediumx, 90);
  penpos3(mediumy, 0);
  penpos41(jointx, -90);

  penpos4(mediumx, -90);

  penpos42(jointx, 90);
  penpos5(mediumy, 0);
  penpos6(mediumx, -90);
  penpos7(mediumx, -90);

  penpos8(mediumy, 0);
  penpos9(mediumy, 0);

if SERIF =  true:
  makeserif(1, z1r, -SERIF_LENGTH, -SERIF_THICKNESS);
  makeserif(2, z9l, -SERIF_LENGTH, SERIF_THICKNESS);
fi
  
  penstroke z1e -- z2e{right}..tension TSN..z3e{down}..tension TSN..z41e{left};
  penstroke z0e -- z4e;
  penstroke z42e{right}..tension TSN..z5e{down}..tension TSN..z6e{left} -- z7e;
  penstroke z8e -- z9e;
  
  penlabels(range 0 thru 99);
endchar;
end
