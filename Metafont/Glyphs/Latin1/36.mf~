input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0024 DOLLAR SIGN - CODE 36
newchar(36, 6pt#, 0);
spine_center_y = 0.5 * CAP_TOP;
  spine_angle:= 12;
  spine_straight_length:= 0.1w;
  spine_ydelta:= 0.25 * X_H;
  beak_spine_xdelta = 0.01w;
  right_terminal_xoffset:=0.1w;
  
  x1r = w - right_terminal_xoffset; y1 = CAP_H - (CAP_H * APERTURE);
  x2 = x4 + beak_spine_xdelta; y2r = CAP_TOP;
  x3r = CURVE_BEARING; y3 = y2l - ((y2l - y41l) / 2);
  x4 = 0.5w; y4 = spine_center_y;
  z41 = z4 shifted(-spine_straight_length, 0) rotatedaround((z4), -spine_angle);
  z42 = z4 shifted(spine_straight_length, 0) rotatedaround((z4), -spine_angle);
  x5l = w - CURVE_BEARING; y5 = y42r - ((y42r - y6r) / 2);
  x6 = x4 - beak_spine_xdelta; y6l = CAP_BOTTOM;
  x7l = x3r; y7 = BASELINE + (CAP_H * APERTURE);

z8 = z2 shifted(0, 1pt);
z9 = z6 shifted(0, -1pt);
penpos8(STEM, 0); penpos9(STEM, 0);
  
  penpos1(BAR, 90 - APERTURE_ANGLE);
  penpos2(BAR, 90);
  penpos3(STEM, 180);
  penpos41(BAR, 270 - spine_angle);
  penpos42(BAR, 270 - spine_angle);
  penpos5(STEM, 180);
  penpos6(BAR, 90);
  penpos7(BAR, 90 - APERTURE_ANGLE);

  if SERIF = true:
    makeserif(1, z1r, -SERIF_THICKNESS, -TERM_LENGTH);
    makeserif(2, z7l,  SERIF_THICKNESS,  TERM_LENGTH);
  fi

  %penstroke z1e..tension TSN..z2e{left}..tension TSN..z3e{down}..tension TSN..z41e{dir -spine_angle} -- z42e{dir -spine_angle}..tension TSN..z5e{down}..tension TSN..z6e{left}..tension TSN..z7e;

  fill z2r{rt}...z1r -- z1l...z2l{lt}..tsn..z3l{dn}..tsn..z41l{dir -spine_angle} -- z42l{dir -spine_angle}..tsn..z5l{dn}..tsn..z6l{lt}...z7l -- z7r...z6r{rt}..tsn..z5r{up}..tsn..z42r{dir (180 - spine_angle)} -- z41r{dir (180 - spine_angle)}..tsn..z3r{up}..tsn..cycle;
  penstroke z8e -- z9e;

  italicize;

  penlabels (range 1 thru 42);
endchar;
end
