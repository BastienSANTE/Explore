% U+0042 LATIN CAPITAL LETTER B - CODE 66
if MONOSPACE = true: WIDTH:= MONO_WIDTH; else: WIDTH:= STRETCH * 6.1pt#; fi
beginchar(66, WIDTH, EM_H, DEPTH);
  stem_x:= 0.2w;
  btension:= 0.9;

  x0 = x1; y0 = y4;
  x1l = LINE_BEARING; y1r = CAP_H;
  x2 = 0.535w; y2r = CAP_H;
  x3 = 0.82w ; y3 = 0.66h;
  x4 = x2; y4 = 0.465h;
  x41 = x2; y41l = y4l;
  x42 = x2; y42l = y4r;
  x5 = 0.88w; y5 = 0.25h;
  x6 = x2; y6r = BASELINE;
  x7 = x8; y7r = BASELINE;
  x8l = LINE_BEARING; y8 = y1r;
  x9l = LINE_BEARING; y9 = y7r;

  penpos0(mediumx, -90);
  
  penpos1(mediumx, 90);
  penpos2(mediumx, 90);
  penpos3(mediumy, 0);
  penpos41(thinx, -90);

  penpos4(mediumx, -90);

  penpos42(thinx, 90);
  penpos5(mediumy, 0);
  penpos6(mediumx, -90);
  penpos7(mediumx, -90);

  penpos8(mediumy, 0);
  penpos9(mediumy, 0);
  
  penstroke z1e -- z2e{right}..tension btension..z3e{down}..tension btension..z41e{left};
  penstroke z0e -- z4e;
  penstroke z42e{right}..tension btension..z5e{down}..tension btension..z6e{left} -- z7e;
  penstroke z8e -- z9e;
  
  penlabels(range 0 thru 99);
endchar;

