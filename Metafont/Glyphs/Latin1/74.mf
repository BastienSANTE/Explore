input ../Config/ExploreSansCFG.mf;

export_setup;

% U+004A LATIN CAPITAL LETTER J - CODE 74
newchar(74, 4.8pt#, 0);
  if MONOSPACE =  true:
    x1r = w - LINE_BEARING; y1 = CAP_H;
    x2r = x1r; y2 = (x2 - x3);
    x3 = 0.5w; y3r = CAP_BOTTOM;
    x4r = LINE_BEARING; y4 = y2;
    x5 = (x3 - x4); y5r = CAP_H;
    x51 = x1; y51r = CAP_H;
  else:
    x1r = w - LINE_BEARING; y1 = CAP_H;
    x2r = x1r; y2 =  0.3 * CAP_H;
    x3 = 0.415w; y3r = CAP_BOTTOM;
    x4 = 0.1w; y4 = 0.18 * CAP_H;
  fi

  penpos1(STEM, 0); penpos2(STEM, 0); penpos3(BAR, -90);

  if SERIF = true:
    makeserif(1, z1l, -SERIF_LENGTH, -SERIF_THICKNESS);
  fi
  
  
  if MONOSPACE = true:
    penpos4(STEM, -90 - APERTURE_ANGLE);
    penpos5(BAR, 90);
    penpos51(BAR, 90);
  else:
    penpos4(BAR, -90 - APERTURE_ANGLE);
  fi
    
  penstroke z1e -- z2e{down}..tension TSN..z3e{left}..tension TSN..z4e;
  if MONOSPACE = true: penstroke z5e -- z51e; fi
  penlabels(range 1 thru 51);
endchar;
end
