outputtemplate := "%c.svg";
outputformat := "svg";

mode_setup;

PROP_WIDTH:= 5.3pt#;
SERIF_C:= 1;

%serifl = SERIF_LENGTH;

input ../Config/ExploreSansCFG.mf;


define_pixels(SERIF_LENGTH);
% U+0061 LATIN SMALL LETTER A - CODE 97
beginchar(97, WIDTH, EM_H, DEPTH);
  x1 = 0.2w; y1 = X_H * (1 - APERTURE);
  x2 = x1 + ((x3 - x1) / 2); y2r = X_TOP;
  x3r = (w - LINE_BEARING); y3 = 0.65 * X_H;
  x31r = x3r; y31 = y8l + ((y7l - y8l) / 2);
  x4r = x3r; y4 = BASELINE;

  x5 = x3; y5 = 0.52 * X_H;
  x6 = x8; y6 = y5;
  x7r = CURVE_BEARING; y7 = y8l + ((y6l - y8l) / 2);

  if mediumx > 0.8pt:
    x8 = x7l + ((x9l - x7l) / 2); y8r = CAP_BOTTOM;
  else:
    x8 = x7 + y7; y8r = CAP_BOTTOM;
  fi
  
  x9l = x31l; y9r = y31r ;
  
  penpos1(mediumx, 90 + APERTURE_ANGLE); penpos2(mediumx, 90); penpos3(mediumy, 0); penpos31(mediumy, 0); penpos4(mediumx, 0);

  penpos5(mediumx, 90); penpos6(mediumx, 90); penpos7(mediumy, 180); penpos8(mediumx, 270); penpos9(thinx, 300);

  if SERIF = true:
    serifp1 = z4;
    serifp2 = serifp1 shifted(SERIF_LENGTH, 0);
    serifp4 = serifp1 shifted(0, SERIF_THICKNESS);
    serifp3 = serifp1 shifted(SERIF_LENGTH, SERIF_THICKNESS);

    serifp5 = z1r;
    serifp6 = serifp5 shifted(SERIF_THICKNESS, 0);
    serifp7 = serifp5 shifted(SERIF_THICKNESS, -SERIF_LENGTH);
    serifp8 = serifp5 shifted(0, -SERIF_LENGTH);
      
    fill serifp1 -- serifp2 -- serifp3 -- serifp4 -- cycle;
    fill serifp5 -- serifp6 -- serifp7 -- serifp8 -- cycle;
    labels(serifp1, serifp2, serifp3, serifp4);
  fi
  %fill z1r..z2r{right}..z3r{down} -- z31r -- z4r -- z4l..tension 2..z31l -- z3l{up}..z2l{left}..z1l -- cycle;
  
  %penstroke z1e..z2e{right}..tension TSN..z3e{down} -- z31e -- z4e;
  %penstroke z5e -- z6e{left}..tension TSN..z7e{down}..tension TSN..z8e{right}..z9e;

  fill z1r..tension TSN..z2r{right}.. tension TSN..z3r{down} -- z4r -- z4l -- z31l -- z3l{up}..tension TSN..z2l{left}..tension TSN..z1l -- cycle;
  fill z5r..z6r{left}..tension TSN..z7r{down}..tension TSN..z8r{right}..z9r -- z9l..z8l{left}..tension TSN..z7l{up}..tension TSN..z6l{right}..z5l -- z5r -- cycle;

  penlabels(range 1 thru 31);  
endchar;
end
