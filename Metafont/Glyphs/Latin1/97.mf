input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0061 LATIN SMALL LETTER A - CODE 97
newchar(97, 5.3pt#, 1);
if ITALIC = false:
  x1 = 0.2w; y1 = X_H * (1 - APERTURE);
  x2 = x1 + ((x3 - x1) / 2); y2r = X_TOP;
  x3r = (w - LINE_BEARING); y3 = 0.65 * X_H;
  x31r = x3r; y31 = y7l - ((y7l - y8l) / 4);
  x4r = x3r; y4 = BASELINE;

  x5 = x3; y5 = 0.52 * X_H;
  x6 = x8; y6 = y5;
  x7r = CURVE_BEARING; y7 = y8l + ((y6l - y8l) / 2);

  if BAR > 0.8pt:
    x8 = 0.45w; y8r = CAP_BOTTOM;
  else:
    x8 = x7 + y7; y8r = CAP_BOTTOM;
  fi

  z81 = z8r shifted(-ARCHCORR, 0);
  
  z9l = z31l ;
else:
  z1r = (w - CURVE_BEARING, X_H);
  z2r = (x1r,  y8);
  z3r = (x1r, BASELINE);

  z4l = (x1l, X_H * (2 / 3));
  z5r = (0.5w, X_TOP);
  z6r = (CURVE_BEARING, 0.5 * X_H);
  z7r = (x5, CAP_BOTTOM);
  z8l = (x1l, X_H / 3);
fi

if ITALIC = false:
  penpos1(BAR, 90 + APERTURE_ANGLE); penpos2(BAR, 90); penpos3(STEM, 0); penpos31(STEM, 0); penpos4(STEM_TERM, 0);

  penpos5(BAR_CROSS, 90); penpos6(BAR_CROSS, 90); penpos7(STEM, 180); penpos8(BAR, 270); penpos9(BAR_THIN, 300);
  fill z1r..tsn..z2r{right}.. tsn..z3r{down} -- z4r -- z4l -- z31l -- z3l{up}..tsn..z2l{left}..tsn..z1l -- cycle;
  
  fill z5r..z6r{left}..tsn..z7r{down}..tsn..z81{right}..tsn..z9r -- z9l..tsn..z8l{left}..tsn..z7l{up}..tsn..z6l{right}..z5l -- z5r -- cycle;
else:
  penpos1(STEM, 0); penpos2(STEM, 0); penpos3(STEM_TERM, 0);
  penpos4(BAR_THIN, aa); penpos5(BAR, 90); penpos6(STEM, 180); penpos7(BAR, 270); penpos8(BAR_THIN, 270 + aa);

  fill z1l -- z1r -- z2r -- z3r -- z3l -- z2l -- z1l -- cycle;
  penstroke z4e...z5e...z6e...z7e..z8e;

fi

  italicize;

  penlabels(range 1 thru 81);  
endchar;
end
