input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0061 LATIN SMALL LETTER A - CODE 97
newchar(97, 5.3pt#, 1);
  x1 = 0.2w; y1 = X_H * (1 - APERTURE);
  x2 = x1 + ((x3 - x1) / 2); y2r = X_TOP;
  x3r = (w - LINE_BEARING); y3 = 0.65 * X_H;
  x31r = x3r; y31 = y8l + ((y7l - y8l) / 2);
  x4r = x3r; y4 = BASELINE;

  x5 = x3; y5 = 0.52 * X_H;
  x6 = x8; y6 = y5;
  x7r = CURVE_BEARING; y7 = y8l + ((y6l - y8l) / 2);

  if BAR > 0.8pt:
    x8 = x7l + ((x9l - x7l) / 2); y8r = CAP_BOTTOM;
  else:
    x8 = x7 + y7; y8r = CAP_BOTTOM;
  fi

  z81 = z8r shifted(-ARCHCORR, 0);
  
  x9l = x31l; y9r = y31r ;
  
  penpos1(BAR_TERM, 90 + APERTURE_ANGLE); penpos2(BAR, 90); penpos3(STEM, 0); penpos31(STEM, 0); penpos4(STEM_TERM, 0);

  penpos5(BAR, 90); penpos6(BAR, 90); penpos7(STEM, 180); penpos8(BAR, 270); penpos9(BAR_THIN, 300);

  if SERIF = true:
    makeserif(1, z1r,  SERIF_THICKNESS, -SERIF_LENGTH);
    makeserif(2, z4r,  SERIF_LENGTH,  SERIF_THICKNESS);
  fi

  fill z1r..tension TSN..z2r{right}.. tension TSN..z3r{down} -- z4r -- z4l -- z31l -- z3l{up}..tension TSN..z2l{left}..tension TSN..z1l -- cycle;
  
  fill z5r..z6r{left}..tension TSN..z7r{down}..tension TSN..z81{right}..z9r -- z9l..z8l{left}..tension TSN..z7l{up}..tension TSN..z6l{right}..z5l -- z5r -- cycle;

  penlabels(range 1 thru 31);  
endchar;
end
