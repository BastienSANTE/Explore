input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0041 LATIN CAPITAL LETTER A - CODE 65
newchar(65, 6.2pt#, 0);
  width:= .75w;
  barcoef:= 0.7; % "Bar coefficient": how low the bar is. Width is automatic.
  x1 = mid; y1 = CAP_H;
  z11 = z1;
  z2l = (OBLIQUE_BEARING, BASELINE);
  z3r = (w - OBLIQUE_BEARING, BASELINE);
  x4 = x1 - (barcoef * (width / 2)); y4 = CAP_H - (barcoef * CAP_H);
  x5 = x1 + (barcoef * (width / 2)); y5 = CAP_H - (barcoef * CAP_H);
  
  penpos1(STEM, 0);
  penpos11(lerpbold(STEM_CROSS, STEM / 2), 0);
  penpos2(STEM_C, 0);
  penpos3(STEM, 0);
  penpos4(BAR_CROSS, 90);
  penpos5(BAR_CROSS, 90);
  
  fill z1 -- z1r -- z3r -- z3l -- z11l -- cycle;
  fill z1l -- z2l -- z2r -- z11r -- cycle;
  fill z4r -- z5r -- z5l -- z4l -- cycle;

  italicize;

  penlabels(range 1 thru 11);
endchar;
end

