input ../Config/ExploreSansCFG.mf;

export_setup;

% U+0041 LATIN CAPITAL LETTER A - CODE 65
newchar(65, 6.2pt#, 0);
  width:= .8w;
  barcoef:= 0.7; % "Bar coefficient": how low the bar is. Width is automatic.
  x1 = .5w; y1 = CAP_H;
  x2 = x1 - width / 2; y2 = BASELINE;
  x3 = x1 + width / 2; y3 = BASELINE;

  x4 = x1 - (barcoef * (width / 2)); y4 = CAP_H - (barcoef * CAP_H);
  x5 = x1 + (barcoef * (width / 2)); y5 = CAP_H - (barcoef * CAP_H);
  
  penpos1(STEM / 1.25, 0);
  penpos2(STEM, 0);
  penpos3(STEM, 0);
  penpos4(BAR, 90);
  penpos5(BAR, 90);

  ikp1 = (x1 + (x1r - x1) / 8, y1 - STEM);
  ikp2 = (x1 - (x1r - x1) / 8, y1 - STEM);
  ikp3 = (x1, y1 - STEM / 2);

  if SERIF = true:
    sfp1 = z1l;
    sfp11 = sfp1 shifted(-SERIF_LENGTH, 0);
    sfp12 = sfp1 shifted(-SERIF_LENGTH, -SERIF_THICKNESS);
    sfp13 = sfp1 shifted(0, -SERIF_THICKNESS);

    fill sfp1 -- sfp11 -- sfp12 -- sfp13 -- cycle;

    sfp2 = z2r;
    sfp21 = sfp2 shifted(-SERIF_LENGTH, 0);
    sfp22 = sfp2 shifted(SERIF_LENGTH, 0);
    sfp23 = sfp2 shifted(SERIF_LENGTH, SERIF_THICKNESS);
    sfp24 = sfp2 shifted(-SERIF_LENGTH, SERIF_THICKNESS);

    fill sfp21 -- sfp22 -- sfp23 -- sfp24 -- cycle;

    sfp3 = z3l;
    sfp31 = sfp3 shifted(-SERIF_LENGTH, 0);
    sfp32 = sfp3 shifted(SERIF_LENGTH, 0);
    sfp33 = sfp3 shifted(SERIF_LENGTH, SERIF_THICKNESS);
    sfp34 = sfp3 shifted(-SERIF_LENGTH, SERIF_THICKNESS);

    fill sfp31 -- sfp32 -- sfp33 -- sfp34 --  cycle;
  fi
  
  fill z1 -- z1r -- z3r -- z3l -- ikp1 -- ikp3 -- z1 -- cycle;
  fill z1 -- z1l -- z2l -- z2r -- ikp2 -- ikp3 -- z1 -- cycle;
  fill z4r -- z5r -- z5l -- z4l -- cycle;

  penlabels(range 1 thru 5);
endchar;
end

