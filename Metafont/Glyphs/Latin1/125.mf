input ../Config/ExploreSansCFG.mf;

export_setup;

% U+007D RIGHT CURLY BRACKET - CODE 125
newchar(125, 3.5pt#, 1);

arc_width = lerpbold(.4pt, .75pt);
if MONOSPACE = true:
  z0r = (CURVE_BEARING, ASCENDER);
  z1r = (w * (1 / 3), ASCENDER);
  z40 = (w - CURVE_BEARING, CAP_H / 2);
  z4 = (x2r + arc_width, CAP_H / 2);
  z9r = (x0, LOWPOINT);

  penpos0(BAR, 90);
  penpos40(BAR, -90);
  penpos9(BAR, -90);
else:  
  z1r = (CURVE_BEARING, ASCENDER);
  z4 = (x2r + arc_width, CAP_H / 2);
fi

z2l = (x1r + arc_width, y1l - arc_width);
z3r = (x2r, y4r + arc_width);
z5r = (x2r, y4l - arc_width);
z6r = (x2r, y7l + arc_width);
z7r = (x1, LOWPOINT);
z42l = z4l; z41r = z4r;

penpos1(BAR, 90);
penpos2(STEM, 0); penpos3(STEM, 0);
penpos4(BAR, 90);
penpos5(STEM, 0); penpos6(STEM, 0);
penpos7(BAR, -90);

penpos41(BAR_JOINT, 90);
penpos42(BAR_JOINT, 90);

if MONOSPACE = true:
  fill z1l -- z0l -- z0r -- z1r{rt}..tsn..z2r{dn} -- z3r{dn}..tsn..z41r{rt} --  z41l{lt}..tsn..z3l{up} -- z2l{up}..tsn..z1l{lt} -- cycle;
  fill z41r -- z40l -- z40r -- z42l -- cycle;
  fill z42l{lt}..tsn..z5r{dn} -- z6r{dn}..tsn..z7r{lt} -- z9r -- z9l -- z7l{rt}..tsn..z6l{up} -- z5l{up}..tsn..z42r{rt} -- cycle;
else:
  fill z1l -- z1r{rt}..tsn..z2r{dn} -- z3r{dn}..tsn..z41r{rt} -- z4l{lt}..tsn..z3l{up} -- z2l{up}..tsn..z1l{lt} -- cycle;
  fill z42l{lt}..tsn..z5r{dn} -- z6r{dn}..tsn..z7r{lt} -- z7l{rt}..tsn..z6l{up} -- z5l{up}..tsn..z42r{rt} -- cycle;
fi

italicize;

penlabels(range 1 thru 42);

endchar;
end
